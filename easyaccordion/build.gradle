apply plugin: 'maven-publish' // Apply this plugin at the top of your library build.gradle
apply plugin: 'com.android.library'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("local.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file

ext {
    PUBLISH_GROUP_ID = 'com.riyagayasen.android'
    PUBLISH_ARTIFACT_ID = 'easyaccordion'
    PUBLISH_VERSION = '1.0.1'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pmachapman/Android_accordion_view")
            credentials {
                username = githubProperties['gpr.user'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        github(MavenPublication) {
            groupId PUBLISH_GROUP_ID // Replace with group ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION
            artifact("$buildDir/outputs/aar/${PUBLISH_ARTIFACT_ID}-release.aar")
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.appcompat:appcompat:1.1.0'
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'